#!/usr/bin/perl -s
use strict;
use warnings;

use Term::ANSIColor qw(:constants);

our ( $n, $h );    # command line switches

if ($h) {
    print << "EOF";
$0 [-h] [-n=N] topic/<file>
    -h    this help
    -n=N  give me N terms (default: all)

Examples:
    ./practice topic/python topic/security
    ./practice -n=5 topic/*
EOF
    exit 0;
}

my $sep = '--->';
my %definition;

while (<>) {
    chomp;
    my ( $expr, $def ) = split /\s*$sep\s*/;
    $definition{$expr} = $def;
}

my %count = ( wrong => 0, right => 0 );

for my $expr ( keys %definition ) {
    last if $n and $count{wrong} + $count{right} == $n;
    next if $expr =~ /^\s*#/;

    print BOLD, BLUE, $expr, RESET, " $sep ";

    chomp( my $ans = <STDIN> );
    if ( $ans eq $definition{$expr} ) {
        $count{right}++;
        print GREEN, "Right!\n", RESET;
    } else {
        $count{wrong}++;
        print "Not really, it's: '$definition{$expr}'\n";
    }
}

print "-" x 80, "\n";
print << "EOF";
[-] wrong: $count{wrong}
[+] right: $count{right}
EOF

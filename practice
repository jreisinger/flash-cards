#!/usr/bin/perl -s
use strict;
use warnings;

use Term::ANSIColor qw(:constants);

our ( $n, $h );    # command line switches

# Help message...
if ($h) {
    print << "EOF";
$0 [-h] [-n=N] topic/<file>
    -h    this help
    -n=N  give me N terms (default: all)

Examples:
    ./practice topic/python topic/security
    ./practice -n=5 topic/*
EOF
    exit 0;
}

my $sep = '--->';
my %definition;

# Read in the terms and their definitions...
while (<>) {
    chomp;
    my ( $term, $def ) = split /\s*$sep\s*/;
    $definition{$term} = $def;
}

my %answers = ( wrong => 0, right => 0 );

for my $term ( keys %definition ) {

    # Skip sub-topics (comments)...
    next if $term =~ /^\s*#/;

    # Give a term and get an answer...
    print BOLD, BLUE, $term, RESET, " $sep ";
    chomp( my $ans = <STDIN> );

    # Provide a hint on empty answer...
    unless ($ans) {
        print "It's: '$definition{$term}'\n";
        redo;
    }

    # Evaluate the answer...
    if ( $ans eq $definition{$term} ) {
        $answers{right}++;
        print GREEN, "Right!\n", RESET;
    } else {
        $answers{wrong}++;
        print "Nope, it's: '$definition{$term}'\n";
    }

    last if $n and $answers{wrong} + $answers{right} >= $n;
}

print "-" x 80, "\n";
print << "EOF";
[-] wrong: $answers{wrong}
[+] right: $answers{right}
EOF
